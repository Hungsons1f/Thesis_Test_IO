<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.4">
  <POU Name="aFB_Valve" Id="{3d1810ca-99c6-4a27-a7c3-1ca372238459}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK aFb_Valve
VAR_INPUT
	Mode: aE_Mode := aE_Mode.Auto;
	
	//Manual
	Start: BOOL;
	Stop: BOOL;
	SetPercent: REAL;
	Reset1: BOOL;
	
	//Auto
	RunCondition: BOOL;
	StopCondition: BOOL;
	PercentCondition: REAL;
	
	//Others
	RunFeedback: BOOL;
	Interlock: BOOL;
	{attribute 'hide'}
	DisplayName: STRING[30];
END_VAR
VAR_OUTPUT
	Cmd: BOOL;
	Fault: BOOL;
	State: aE_State;
	OpenPercent: REAL;
	Runtime: UDINT := 0;
	AccRuntime: UDINT := 0;
END_VAR
VAR
	{attribute 'hide'}
	fbTon: TON;
	{attribute 'hide'}
	fbTRuntime: TON;	
	
	{attribute 'hide'}
	fbSource: FB_TcSourceInfo;
	{attribute 'hide'}
	fbMesModeMan: FB_TcMessage;
	{attribute 'hide'}
	fbMesModeAuto: FB_TcMessage;
	{attribute 'hide'}
	fbMesModeServ: FB_TcMessage;
	{attribute 'hide'}
	fbMesStart: FB_TcMessage;
	{attribute 'hide'}
	fbMesStop: FB_TcMessage;
	{attribute 'hide'}
	fbMesReset: FB_TcMessage;
	{attribute 'hide'}
	fbMesFault: FB_TcMessage;
	
	{attribute 'hide'}
	bFirstScan: BOOL := TRUE;
	{attribute 'hide'}
	trigModeMan: R_TRIG;
	{attribute 'hide'}
	trigModeAuto: R_TRIG;
	{attribute 'hide'}
	trigModeServ: R_TRIG;
	{attribute 'hide'}
	trigStart: R_TRIG;
	{attribute 'hide'}
	trigStop: R_TRIG;
	{attribute 'hide'}
	trigReset: R_TRIG;
	{attribute 'hide'}
	trigFault: R_TRIG;

END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[IF bFirstScan THEN
	bFirstScan := FALSE;
	fbSource.sName := DisplayName;
	fbMesModeMan.CreateEx(TC_Events.ComponentEvent.ModeManual, fbSource);
	fbMesModeAuto.CreateEx(TC_Events.ComponentEvent.ModeAutomatic, fbSource);
	fbMesModeServ.CreateEx(TC_Events.ComponentEvent.ModeService, fbSource);
	fbMesStart.CreateEx(TC_Events.ComponentEvent.Started, fbSource);
	fbMesStop.CreateEx(TC_Events.ComponentEvent.Stopped, fbSource);
	fbMesReset.CreateEx(TC_Events.ComponentEvent.Resetted, fbSource);
	fbMesFault.CreateEx(TC_Events.ComponentEvent.Fault, fbSource);
END_IF

CASE Mode OF 
	aE_Mode.Manual: 
		Cmd := (Cmd OR Start) AND NOT Stop AND NOT Fault AND NOT Interlock;
		IF Cmd THEN
			State := aE_State.ManRun;
		ELSE
			State := aE_State.ManStop;
		END_IF
	aE_Mode.Auto:
		Cmd := (Cmd OR RunCondition) AND NOT StopCondition AND NOT Fault AND NOT Interlock;
		State := aE_State.Auto;
	aE_Mode.Service:
		Cmd := (Cmd OR Start) AND NOT Stop AND NOT Fault;
END_CASE

IF Cmd THEN
	IF Mode = aE_Mode.Auto THEN
		OpenPercent := LIMIT( 0, PercentCondition, 1);
	ELSE
		OpenPercent := LIMIT( 0, SetPercent, 1);
	END_IF
ELSE 
	OpenPercent:= 0;
END_IF

fbTon(IN := Cmd, PT := T#3S);
IF fbTon.Q AND NOT RunFeedback THEN
	Fault := TRUE;
	State := aE_State.Fault;
END_IF

IF Reset1 THEN
	Fault := FALSE;
END_IF

IF Cmd THEN
	fbTRuntime(IN := NOT fbTRuntime.Q, PT:= T#1S);
	IF fbTRuntime.Q THEN
		Runtime:= Runtime + 1;
		AccRuntime := AccRuntime + 1;
	END_IF
ELSE
	Runtime:= 0;
END_IF

trigModeMan(CLK:= (Mode = aE_Mode.Manual));
trigModeAuto(CLK:= (Mode = aE_Mode.Auto));
trigModeServ(CLK:= (Mode = aE_Mode.Service));
trigStart(CLK:= Start);
trigStop(CLK := Stop);
trigReset(CLK := Reset1);
trigFault(CLK := Fault);

IF trigModeMan.Q THEN
	fbMesModeMan.Send(0);
END_IF
IF trigModeAuto.Q THEN
	fbMesModeAuto.Send(0);
END_IF
IF trigModeServ.Q THEN
	fbMesModeServ.Send(0);
END_IF
IF trigStart.Q THEN
	fbMesStart.Send(0);
END_IF
IF trigStop.Q THEN
	fbMesStop.Send(0);
END_IF
IF trigReset.Q THEN
	fbMesReset.Send(0);
END_IF
IF trigFault.Q THEN
	fbMesFault.Send(0);
END_IF]]></ST>
    </Implementation>
    <LineIds Name="aFB_Valve">
      <LineId Id="226" Count="9" />
      <LineId Id="224" Count="1" />
      <LineId Id="37" Count="2" />
      <LineId Id="134" Count="4" />
      <LineId Id="41" Count="1" />
      <LineId Id="140" Count="0" />
      <LineId Id="139" Count="0" />
      <LineId Id="45" Count="0" />
      <LineId Id="47" Count="0" />
      <LineId Id="85" Count="0" />
      <LineId Id="87" Count="8" />
      <LineId Id="48" Count="3" />
      <LineId Id="141" Count="0" />
      <LineId Id="52" Count="3" />
      <LineId Id="9" Count="0" />
      <LineId Id="144" Count="8" />
      <LineId Id="143" Count="0" />
      <LineId Id="237" Count="28" />
      <LineId Id="236" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>