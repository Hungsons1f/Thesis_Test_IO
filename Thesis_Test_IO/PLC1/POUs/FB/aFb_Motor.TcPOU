<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.4">
  <POU Name="aFb_Motor" Id="{deea6eae-00c4-4d52-adc2-0d6a9e27c1a4}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK aFb_Motor
VAR_INPUT
	Mode: aE_Mode := aE_Mode.Auto;
	
	//Manual
	Start: BOOL;
	Stop: BOOL;
	SetSpeed: REAL;
	Reset1: BOOL;
	
	//Auto
	RunCondition: BOOL;
	StopCondition: BOOL;
	SpeedCondition: REAL;
	
	//Others
	RunFeedback: BOOL;
	Interlock: BOOL;
	Maxspeed: REAL:= 1500;
	ActualSpeed: REAL;
	{attribute 'hide'}
	DisplayName: STRING[20];
END_VAR
VAR_OUTPUT
	Cmd: BOOL;
	Fault: BOOL;
	State: aE_State;
	Speed: REAL;
	Runtime: UDINT := 0;
	AccRuntime: UDINT := 0;
END_VAR
VAR
	{attribute 'hide'}
	fbTon: TON;
	{attribute 'hide'}
	fbTRuntime: TON;
	
	{attribute 'hide'}
	fbSource: FB_TcSourceInfo;
	{attribute 'hide'}
	fbMesModeMan: FB_TcMessage;
	{attribute 'hide'}
	fbMesModeAuto: FB_TcMessage;
	{attribute 'hide'}
	fbMesModeServ: FB_TcMessage;
	{attribute 'hide'}
	fbMesStart: FB_TcMessage;
	{attribute 'hide'}
	fbMesStop: FB_TcMessage;
	{attribute 'hide'}
	fbMesReset: FB_TcMessage;
	{attribute 'hide'}
	fbMesFault: FB_TcMessage;
	
	{attribute 'hide'}
	bFirstScan: BOOL := TRUE;
	{attribute 'hide'}
	trigModeMan: R_TRIG;
	{attribute 'hide'}
	trigModeAuto: R_TRIG;
	{attribute 'hide'}
	trigModeServ: R_TRIG;
	{attribute 'hide'}
	trigStart: R_TRIG;
	{attribute 'hide'}
	trigStop: R_TRIG;
	{attribute 'hide'}
	trigReset: R_TRIG;
	{attribute 'hide'}
	trigFault: R_TRIG;
	
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[IF bFirstScan THEN
	bFirstScan := FALSE;
	fbSource.sName := DisplayName;
	fbMesModeMan.CreateEx(TC_Events.ComponentEvent.ModeManual, fbSource);
	fbMesModeAuto.CreateEx(TC_Events.ComponentEvent.ModeAutomatic, fbSource);
	fbMesModeServ.CreateEx(TC_Events.ComponentEvent.ModeService, fbSource);
	fbMesStart.CreateEx(TC_Events.ComponentEvent.Started, fbSource);
	fbMesStop.CreateEx(TC_Events.ComponentEvent.Stopped, fbSource);
	fbMesReset.CreateEx(TC_Events.ComponentEvent.Resetted, fbSource);
	fbMesFault.CreateEx(TC_Events.ComponentEvent.Fault, fbSource);
END_IF

CASE Mode OF 
	aE_Mode.Manual: 
		Cmd := (Cmd OR Start) AND NOT Stop AND NOT Fault AND NOT Interlock;
		IF Cmd THEN
			State := aE_State.ManRun;
		ELSE
			State := aE_State.ManStop;
		END_IF
	aE_Mode.Auto:
		Cmd := (Cmd OR RunCondition) AND NOT StopCondition AND NOT Fault AND NOT Interlock;
		State := aE_State.Auto;
	aE_Mode.Service:
		Cmd := (Cmd OR Start) AND NOT Stop AND NOT Fault;
		IF Cmd THEN
			State := aE_State.ManRun;
		ELSE
			State := aE_State.ManStop;
		END_IF
END_CASE

IF Cmd THEN
	IF Mode = aE_Mode.Auto THEN
		Speed := LIMIT(-Maxspeed, SpeedCondition, Maxspeed);
	ELSE
		Speed := LIMIT(-Maxspeed, SetSpeed, Maxspeed);
	END_IF
ELSE 
	Speed:= 0;
END_IF

fbTon(IN := Cmd, PT := T#5S);
IF fbTon.Q AND NOT RunFeedback THEN
	Fault := TRUE;
	State := aE_State.Fault;
END_IF

IF Reset1 THEN
	Fault := FALSE;
END_IF

IF Cmd THEN
	fbTRuntime(IN := NOT fbTRuntime.Q, PT:= T#1S);
	IF fbTRuntime.Q THEN
		Runtime:= Runtime + 1;
		AccRuntime := AccRuntime + 1;
	END_IF
ELSE
	Runtime:= 0;
END_IF

trigModeMan(CLK:= (Mode = aE_Mode.Manual));
trigModeAuto(CLK:= (Mode = aE_Mode.Auto));
trigModeServ(CLK:= (Mode = aE_Mode.Service));
trigStart(CLK:= Start);
trigStop(CLK := Stop);
trigReset(CLK := Reset1);
trigFault(CLK := Fault);

IF trigModeMan.Q THEN
	fbMesModeMan.Send(0);
END_IF
IF trigModeAuto.Q THEN
	fbMesModeAuto.Send(0);
END_IF
IF trigModeServ.Q THEN
	fbMesModeServ.Send(0);
END_IF
IF trigStart.Q THEN
	fbMesStart.Send(0);
END_IF
IF trigStop.Q THEN
	fbMesStop.Send(0);
END_IF
IF trigReset.Q THEN
	fbMesReset.Send(0);
END_IF
IF trigFault.Q THEN
	fbMesFault.Send(0);
END_IF]]></ST>
    </Implementation>
    <LineIds Name="aFb_Motor">
      <LineId Id="397" Count="0" />
      <LineId Id="402" Count="0" />
      <LineId Id="461" Count="0" />
      <LineId Id="404" Count="6" />
      <LineId Id="400" Count="0" />
      <LineId Id="398" Count="0" />
      <LineId Id="48" Count="2" />
      <LineId Id="322" Count="1" />
      <LineId Id="325" Count="1" />
      <LineId Id="324" Count="0" />
      <LineId Id="52" Count="1" />
      <LineId Id="318" Count="0" />
      <LineId Id="55" Count="1" />
      <LineId Id="535" Count="4" />
      <LineId Id="58" Count="0" />
      <LineId Id="114" Count="0" />
      <LineId Id="116" Count="1" />
      <LineId Id="247" Count="0" />
      <LineId Id="123" Count="0" />
      <LineId Id="277" Count="0" />
      <LineId Id="121" Count="0" />
      <LineId Id="125" Count="1" />
      <LineId Id="118" Count="0" />
      <LineId Id="59" Count="3" />
      <LineId Id="321" Count="0" />
      <LineId Id="63" Count="3" />
      <LineId Id="9" Count="0" />
      <LineId Id="332" Count="0" />
      <LineId Id="328" Count="0" />
      <LineId Id="333" Count="0" />
      <LineId Id="335" Count="1" />
      <LineId Id="338" Count="0" />
      <LineId Id="337" Count="0" />
      <LineId Id="340" Count="1" />
      <LineId Id="334" Count="0" />
      <LineId Id="414" Count="0" />
      <LineId Id="413" Count="0" />
      <LineId Id="431" Count="5" />
      <LineId Id="438" Count="0" />
      <LineId Id="437" Count="0" />
      <LineId Id="439" Count="1" />
      <LineId Id="442" Count="1" />
      <LineId Id="441" Count="0" />
      <LineId Id="445" Count="1" />
      <LineId Id="444" Count="0" />
      <LineId Id="448" Count="1" />
      <LineId Id="447" Count="0" />
      <LineId Id="451" Count="1" />
      <LineId Id="450" Count="0" />
      <LineId Id="454" Count="1" />
      <LineId Id="453" Count="0" />
      <LineId Id="457" Count="1" />
      <LineId Id="456" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>